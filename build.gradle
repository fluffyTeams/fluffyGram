plugins {
    id 'com.android.application' version '8.7.3' apply false
    id 'com.google.gms.google-services' version '4.4.2' apply false
    id 'org.lsposed.lsparanoid' version '0.5.2'
}

subprojects {
    afterEvaluate {
        if (plugins.hasPlugin("com.android.library") ||
                plugins.hasPlugin("com.android.application")) {
            android {
                compileSdkVersion 34
                buildToolsVersion "34.0.0"
                ndkVersion "27.2.12479018"

                lint {
                    checkReleaseBuilds false
                    disable 'MissingTranslation', 'ExtraTranslation', 'BlockedPrivateApi'
                }

                sourceSets.main {
                    jniLibs.srcDirs = ['./jni/']
                    res.srcDirs = ['src/main/res', 'src/main/res-solar']
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17

                    coreLibraryDesugaringEnabled true
                }

                defaultConfig {
                    minSdkVersion 26
                    targetSdkVersion 34
                }

                buildTypes {
                    play {
                        initWith release
                    }
                }

                buildFeatures.buildConfig = true
            }

            dependencies {
                coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs_nio:2.0.4'
            }
        }
    }
}
